import("//brave/components/common/typescript.gni")
import("//tools/grit/grit_rule.gni")
import("//tools/grit/repack.gni")

pack_web_resources("internals_generated_resources") {
  resource_name = "tor_internals"
  output_dir = "$root_gen_dir/brave/components/tor/resources"

  # need webpack and GRD generation first
  deps = [ "internals" ]
}

grit("static_resources") {
  source = "tor_static_resources.grd"

  outputs = [
    "grit/tor_resources.h",
    "tor_static.pak",
  ]

  grit_flags = [
    "-E",
    "root_gen_dir=" + rebase_path(root_gen_dir, root_build_dir),
  ]

  resource_ids = "//brave/browser/resources/resource_ids"

  output_dir = "$root_gen_dir/brave/components/tor/resources"
}

repack("resources") {
  public_deps = [
    ":internals_generated_resources",
    ":static_resources",
  ]

  sources = [
    "$root_gen_dir/brave/components/tor/resources/tor_internals_generated.pak",
    "$root_gen_dir/brave/components/tor/resources/tor_static.pak",
  ]

  output = "$root_gen_dir/brave/components/tor/resources/tor_resources.pak"
}
